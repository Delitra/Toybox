# SK's Paint: HSV/RGB Selector
# Copyright (c) 2009 sk89q <http://sk89q.therisenrealm.com>
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
# 
# $Id$

@name HSV/RGB Color Selector [sk89q]
@inputs CursorX CursorY Use Reset Screen:wirelink RGBOverride:vector Override
@outputs R G B RGB:vector H S V

ScreenWidth = 12
ScreenHeight = 12
VSelWidth = 1

if (~Reset) {
    if (Reset) {
        for (I = 0, ScreenWidth*ScreenHeight) {
            Screen[I] = 0
        }
    }
} elseif (~Override) {
    if (Override) {
        RGB = RGBOverride
        R = RGBOverride:x()
        G = RGBOverride:y()
        B = RGBOverride:z()
        H = -1
        S = -1
        V = -1
    }
} elseif (Use) {
    if (CursorX <= (ScreenWidth - VSelWidth) / ScreenWidth) {
        H = 360 * CursorX / (1 - VSelWidth / ScreenWidth)
        S = CursorY
        V = 1
        Hi = floor(H / 60) % 6
        F = H / 60 - floor(H / 60)
        P = floor(255 * V * (1 - S))
        Q = floor(255 * V * (1 - F * S))
        T = floor(255 * V * (1 - (1 - F) * S))
        V1 = floor(255 * V)
        if (Hi == 0) {     RGB = vec(V1, T, P) }
        elseif (Hi == 1) { RGB = vec(Q, V1, P) }
        elseif (Hi == 2) { RGB = vec(P, V1, T) }
        elseif (Hi == 3) { RGB = vec(P, Q, V1) }
        elseif (Hi == 4) { RGB = vec(T, P, V1) }
        elseif (Hi == 5) { RGB = vec(V1, P, Q) }
        R = RGB:x()
        G = RGB:y()
        B = RGB:z()
    } else {
        RGB = vec(0, 0, 0)
        R = 0
        G = 0
        B = 0
    }
}
